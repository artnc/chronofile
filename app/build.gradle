apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// https://developer.android.com/studio/publish/app-signing#secure_key
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
  signingConfigs {
    config {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }

  namespace 'com.chaidarun.chronofile'
  compileSdkVersion 29

  defaultConfig {
    applicationId 'com.chaidarun.chronofile'
    minSdkVersion 26
    targetSdkVersion 29 // TODO: Bump after removing dependency on deprecated [READ/WRITE]_EXTERNAL_STORAGE perms
    versionCode 4
    versionName '1.0.3'
  }

  buildFeatures {
    viewBinding true
  }

  buildTypes {
    debug {
      minifyEnabled false
      signingConfig signingConfigs.config
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.config
    }
  }
}

dependencies {
  implementation 'androidx.appcompat:appcompat:1.3.0'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.recyclerview:recyclerview:1.2.1'
  implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
  implementation 'com.google.android.gms:play-services-location:21.0.1'
  implementation 'com.google.android.material:material:1.4.0'
  implementation 'com.google.code.gson:gson:2.11.0'
  implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.0.0'
  implementation 'com.jakewharton.rxbinding2:rxbinding-design-kotlin:2.0.0'
  implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0'
  implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.0.0'
  implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
  implementation 'io.github.inflationx:calligraphy3:3.1.1'
  implementation 'io.github.inflationx:viewpump:2.0.3'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
